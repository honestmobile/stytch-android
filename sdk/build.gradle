plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id "org.jetbrains.dokka"
}

ext {
  PUBLISH_GROUP_ID = 'com.stytch.sdk'
  PUBLISH_VERSION = '0.7.0'
  PUBLISH_ARTIFACT_ID = 'sdk'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "Boolean", "DEBUG_MODE", "false"
            buildConfigField "String", "STYTCH_SDK_VERSION", "\"$PUBLISH_VERSION\""
        }
        debug {
            minifyEnabled false
            buildConfigField "Boolean", "DEBUG_MODE", "true"
            buildConfigField "String", "STYTCH_SDK_VERSION", "\"$PUBLISH_VERSION\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-Xexplicit-api=strict'
    }
    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
            all {
                it.systemProperty("robolectric.dependency.repo.id'", "mavenCentral")
                it.systemProperty("robolectric.dependency.repo.url", "https://repo1.maven.org/maven2")
            }
        }
    }
    lintOptions {
        disable 'OldTargetApi'
    }
    testVariants.all {
        it.mergedFlavor.manifestPlaceholders = [
                'stytchOAuthRedirectScheme': 'test://',
                'stytchOAuthRedirectHost': 'oauth'
        ]
    }
    unitTestVariants.all {
        it.mergedFlavor.manifestPlaceholders = [
            'stytchOAuthRedirectScheme': 'test://',
            'stytchOAuthRedirectHost': 'oauth'
        ]
    }
}

tasks.named("dokkaHtml").configure {
    moduleName.set("Stytch Kotlin")
    suppressInheritedMembers.set(true)
    dokkaSourceSets {
        configureEach {
            perPackageOption {
                matchingRegex.set(".*\\.network.*")
                suppress.set(true)
            }
            reportUndocumented.set(true)
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.biometric:biometric-ktx:1.2.0-alpha05"
    implementation "androidx.browser:browser:1.4.0"
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'com.google.android.gms:play-services-auth:20.4.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation("com.squareup.okhttp3:okhttp:$okhttp_version")
    implementation "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"
    implementation 'com.google.crypto.tink:tink-android:1.7.0'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.72'


    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-core:4.6.1"
    testImplementation "io.mockk:mockk:1.12.5"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation 'org.robolectric:robolectric:4.9.2'
    testImplementation("com.squareup.okhttp3:mockwebserver:4.7.2")
}
